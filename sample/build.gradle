apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.getkeepsafe.dexcount'

static def getGitCommitCount() {
    def process = "git rev-list HEAD --count".execute()
    return process.text.toInteger()
}

def version = '1.0'

android {
    signingConfigs {
        release {
            keyAlias 'test'
            keyPassword 'testtest'
            storeFile file('test_release_keystore.jks')
            storePassword 'testtest'
        }
    }
    compileSdkVersion defaultCompileSdkVersion
    defaultConfig {
        applicationId "info.dbprefes.sample"
        minSdkVersion defaultMinSdkVersion
        targetSdkVersion defaultTargetSdkVersion
        versionCode getGitCommitCount()
        versionName version + "." + versionCode

        multiDexEnabled true
        testInstrumentationRunner = 'androidx.test.runner.AndroidJUnitRunner'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    dataBinding.enabled = true
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    testBuildType "release"
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix ".debug"
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), "$projectDir/proguard-rules.pro"
            //test apk will have proguard applied to it so we detect any crashes or anomalies during espresso tests
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), "$projectDir/proguard-rules.pro", "$projectDir/proguard-test-rules.pro"
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "historic"
    productFlavors {
        useOldLib { // build with recent from jitpack.io
            dimension "historic"
        }
        useRecentCode { // build with recent code
            dimension "historic"
        }
    }
}

dependencies {
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'androidx.multidex:multidex:2.0.1'    // minsdk <= 20
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    useRecentCodeImplementation project(':DBPrefs')
    //noinspection GradleDependency
    useOldLibImplementation "com.github.hannesa2:DbPreferences:4.1"

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    //  UI testing with Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}
