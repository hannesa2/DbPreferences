apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'org.jetbrains.dokka-android'

String dbPrefsVersionName = "2.0"
android {
    compileSdkVersion defaultCompileSdkVersion

    defaultConfig {
        minSdkVersion defaultMinSdkVersion
        targetSdkVersion defaultTargetSdkVersion
        versionCode 1
        versionName dbPrefsVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // For Instrumentation Unit testing
        consumerProguardFile('proguard-rules.pro')
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            groupId = 'com.github.hannesa2'
            artifactId = 'DbPreferences'
            version = dbPrefsVersionName
            from components.android
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.code.gson:gson:2.8.5'

    implementation "com.commonsware.cwac:saferoom.x:0.5.0"
    implementation "androidx.room:room-runtime:$room"
    kapt "androidx.room:room-compiler:$roomruntime"
    implementation "androidx.room:room-rxjava2:$room"

    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"

    lintChecks project(':DBPrefs-lint')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation "androidx.room:room-testing:$room"
    androidTestImplementation 'androidx.test:runner:1.1.0-beta02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-beta02'
    androidTestImplementation 'androidx.test.ext:junit:1.0.0-beta02'
}

dokka {
    moduleName = 'DbPreferences'
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"

    // List of files with module and package documentation
    // http://kotlinlang.org/docs/reference/kotlin-doc.html#module-and-package-documentation
    includes = ['README.md']

    // Use to include or exclude non public members.
    includeNonPublic = false

    // Do not output deprecated members. Applies globally, can be overridden by packageOptions
    skipDeprecated = true

    // Emit warnings about not documented members. Applies globally, also can be overridden by packageOptions
    reportUndocumented = true

    skipEmptyPackages = true // Do not create index pages for empty packages
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
    archiveName = "javadoc.jar"
}

artifacts {
    archives javadocJar
}
